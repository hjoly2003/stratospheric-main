plugins {
  id 'org.springframework.boot' version '3.1.2'
  id 'io.spring.dependency-management' version '1.1.3'
  id 'java'
}

group = 'dev.stratospheric'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
  mavenCentral()
}

ext {
  set('awsSpringVersion', '3.0.1')
  set('testcontainersVersion', '1.18.3')
  set('jsr305Version', '3.0.2')
}

dependencies {
  // [N]:security
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

  // [N] Spring Boot Starter Thymeleaf: This starter includes all relevant Thymeleaf dependencies and auto-configures the ViewResolver to render Thymeleaf templates.
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

  // [N] Spring Boot Starter Web: The core of our application. It includes and autoconfigures Tomcat as an embedded Servlet container.
  implementation 'org.springframework.boot:spring-boot-starter-web'

  // [!]
  implementation 'org.springframework.boot:spring-boot-starter-mail'

  // [N]:jpa - To connect to the PostgreSQL database instance.
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

  // [N]:security
  implementation 'org.springframework.boot:spring-boot-starter-security'

  // [N] Spring Boot Starter Validation: Used to validate incoming payloads using Bean Validation.
  implementation 'org.springframework.boot:spring-boot-starter-validation'

  implementation "com.google.code.findbugs:jsr305:${jsr305Version}"
  
  // [N]:ses - Spring Cloud AWS::SES dependency
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-ses'

  // [N]:sqs
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-sqs'

  // [N]:webjars - For the JavaScript and CSS libraries that our frontend requires (see ttps://www.webjars.org/).
  implementation 'org.webjars:webjars-locator-core'
  implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
  implementation 'org.webjars:bootstrap:5.3.1'
  implementation 'org.webjars:font-awesome:6.4.0'
  
  // [N]:flyway
  implementation 'org.flywaydb:flyway-core'

  // [N]:security]:cognito - Used by Spring Cloud AWS to align all AWS Java SDK versions for communicating with Cognito.
  implementation 'software.amazon.awssdk:cognitoidentityprovider'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  // Fixing Console Warning on M1 Processors
  implementation group: 'io.netty', name: 'netty-resolver-dns-native-macos', classifier: 'osx-aarch_64'

  // [N]:jpa - To connect to the PostgreSQL database instance. It contains the JDBC driver for connecting to the PostgreSQL database.
  runtimeOnly 'org.postgresql:postgresql'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.testcontainers:localstack'
  testImplementation 'org.testcontainers:junit-jupiter'

  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

dependencyManagement {
  imports {
    // [N] The Spring Cloud AWS BOM ensures that all (upcoming) dependencies that are part of the Spring Cloud AWS umbrella share the same version.
    mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:${awsSpringVersion}"

    mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
  }
}

test {
  useJUnitPlatform()
}

bootRun {
  jvmArgs = [
    "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005",
    // [N]:local - Defines the Spring active profile to "dev" for local development
    "-Dspring.profiles.active=dev",
  ]
}

jar {
  enabled = false
}
