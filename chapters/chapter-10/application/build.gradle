plugins {
  id 'org.springframework.boot' version '3.1.1'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'java'
}

group = 'dev.stratospheric'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
  mavenCentral()
}

ext {
  set('awsSpringVersion', '3.0.1')
  set('testcontainersVersion', '1.18.1')
  set('thymeleafExtrasSpringSecurityVersion', '3.1.1.RELEASE')
  set('jsr305Version', '3.0.2')
}

dependencies {

  // [N] Spring Boot Starter Thymeleaf: This starter includes all relevant Thymeleaf dependencies and auto-configures the ViewResolver to render Thymeleaf templates.
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

  // [N] Spring Boot Starter Validation: Used to validate incoming payloads using Bean Validation.
  implementation 'org.springframework.boot:spring-boot-starter-validation'


  // [N] Spring Boot Starter Web: The core of our application. It includes and autoconfigures Tomcat as an embedded Servlet container.
  implementation 'org.springframework.boot:spring-boot-starter-web'
  // [N]:security
  implementation 'org.springframework.boot:spring-boot-starter-security'
  // [N]:security
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

  // [N] Spring Cloud Starter AWS: The main integration point for integrating our Spring Boot application with AWS.
  implementation 'io.awspring.cloud:spring-cloud-aws-starter'

  // [N]:cognito - For communicating with Cognito
  implementation 'software.amazon.awssdk:cognitoidentityprovider'

  implementation "org.thymeleaf.extras:thymeleaf-extras-springsecurity6:${thymeleafExtrasSpringSecurityVersion}"

  // [N]:webjars - For the JavaScript and CSS libraries that our frontend requires (see ttps://www.webjars.org/).
  implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
  implementation "com.google.code.findbugs:jsr305:${jsr305Version}"
  implementation 'org.webjars:webjars-locator-core'
  implementation 'org.webjars:bootstrap:5.3.0'
  implementation 'org.webjars:font-awesome:6.4.0'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.testcontainers:localstack'
  testImplementation 'org.testcontainers:junit-jupiter'

  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

dependencyManagement {
  imports {
    // [N] The Spring Cloud AWS BOM ensures that all (upcoming) dependencies that are part of the Spring Cloud AWS umbrella share the same version.
    mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:${awsSpringVersion}"

    mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
  }
}

test {
  useJUnitPlatform()
}

bootRun {
  jvmArgs = [
    "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005",
    "-Dspring.profiles.active=dev",
  ]
}

jar {
  enabled = false
}
