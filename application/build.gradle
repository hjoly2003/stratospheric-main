plugins {
  id 'org.springframework.boot' version '3.1.2'
  id 'io.spring.dependency-management' version '1.1.3'
  id 'com.gorylenko.gradle-git-properties' version '2.4.1'
  id 'java'
}

group = 'dev.stratospheric'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
  mavenCentral()
  mavenLocal()
}

ext {
  set('selenium.version', '4.9.1')
  set('httpclient5.version', '5.2.1')
  set('awsSpringVersion', '3.0.1')
  set('testcontainersVersion', '1.18.3')
  set('logbackAwslogsJsonEncoderVersion', '1.1.1')
  set('errorHandlingSpringBootStarterVersion', '4.1.0')
  set('selenideVersion', '6.15.0')
}

dependencies {
  // [N]:security
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

  // [N] Spring Boot Starter Thymeleaf: This starter includes all relevant Thymeleaf dependencies and auto-configures the ViewResolver to render Thymeleaf templates.
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

  // [N] Spring Boot Starter Web: The core of our application. It includes and autoconfigures Tomcat as an embedded Servlet container.
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'

  // [N]:jpa - To connect to the PostgreSQL database instance.
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

  // [N]:mq]:websocket
  implementation 'org.springframework.boot:spring-boot-starter-websocket'

  // [N] Spring Boot Starter Validation: Used to validate incoming payloads using Bean Validation.
  implementation 'org.springframework.boot:spring-boot-starter-validation'

  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  
  // [N]:ses - Spring Cloud AWS::SES dependency
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-ses'

  // [N]:sqs
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-sqs'

  // [N]:nosql
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-dynamodb'

  implementation 'io.micrometer:micrometer-registry-cloudwatch'
  
  // [N]:mq
  implementation 'org.springframework.boot:spring-boot-starter-activemq'
  
  // [N]:mq]:stomp
  implementation 'org.apache.activemq:activemq-stomp'
  implementation 'org.apache.activemq:activemq-kahadb-store'
  
  // [N]:webjars - For the JavaScript and CSS libraries that our frontend requires (see ttps://www.webjars.org/).
  implementation 'org.webjars:webjars-locator-core'
  implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'

  // [N]:websocket]:stomp]:statefull - Provides the SockJS WebSocket client and STOMP support on the client side.
  implementation 'org.webjars:sockjs-client:1.5.1'
  implementation 'org.webjars:stomp-websocket:2.3.4'

  implementation 'org.webjars:bootstrap:5.3.1'
  implementation 'org.webjars:popper.js:2.11.7'
  implementation 'org.webjars:jquery:3.7.1'
  implementation 'org.webjars:font-awesome:6.4.0'

  // [N]:flyway
  implementation 'org.flywaydb:flyway-core'

  // [N]:security]:cognito - Used by Spring Cloud AWS to align all AWS Java SDK versions for communicating with Cognito.
  implementation 'software.amazon.awssdk:cognitoidentityprovider'

  implementation "de.siegmar:logback-awslogs-json-encoder:${logbackAwslogsJsonEncoderVersion}"
  implementation "io.github.wimdeblauwe:error-handling-spring-boot-starter:${errorHandlingSpringBootStarterVersion}"

  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  // Fixing Console Warning on M1 Processors
  implementation group: 'io.netty', name: 'netty-resolver-dns-native-macos', classifier: 'osx-aarch_64'

  // [N]:jpa - To connect to the PostgreSQL database instance. It contains the JDBC driver for connecting to the PostgreSQL database.
  runtimeOnly 'org.postgresql:postgresql'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'org.testcontainers:localstack'
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.testcontainers:postgresql'
  testImplementation "com.codeborne:selenide:${selenideVersion}"
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

dependencyManagement {
  imports {
    // [N] The Spring Cloud AWS BOM ensures that all (upcoming) dependencies that are part of the Spring Cloud AWS umbrella share the same version.
    mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:${awsSpringVersion}"

    mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
  }
}

test {
  useJUnitPlatform()
}

bootRun {
  jvmArgs = [
    "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005",
    // [N]:local - Defines the Spring active profile to "dev" for local development
    "-Dspring.profiles.active=dev",
  ]
}

gitProperties {
}

jar {
  enabled = false
}
